apply plugin: 'maven-publish'

java {
    withSourcesJar()
    withJavadocJar()
}

def githubUrl = 'https://github.com/DanielLiu1123/grpc-starter'

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            // see https://docs.gradle.org/current/userguide/publishing_maven.html
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                url = "${githubUrl}"
                name = project.name
                description = project.description ?: project.name
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/license/mit'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'Freeman'
                        name = 'Freeman Liu'
                        email = 'freemanlau1228@gmail.com'
                    }
                }
                scm {
                    connection = "scm:git:git://${githubUrl.substring(8)}.git"
                    developerConnection = "scm:git:ssh@${githubUrl.substring(8)}.git"
                    url = "${githubUrl}"
                }
                withXml {
                    // Generate optional dependencies for optional configuration
                    def dependencies = asNode().dependencies[0] ?: asNode().appendNode("dependencies")

                    def resolvedDeps = project.configurations.optional.resolvedConfiguration.resolvedArtifacts
                            .stream()
                            .map(e -> e.moduleVersion.id)
                            .collect(java.util.stream.Collectors.toMap(dep -> dep.group + ":" + dep.name, dep -> dep.version));

                    for (dep in project.configurations.optional.allDependencies) {
                        def version = resolvedDeps[dep.group + ":" + dep.name]
                        if (dep.group && dep.name && version) {
                            def dependency = dependencies.appendNode("dependency")
                            dependency.appendNode("groupId", dep.group)
                            dependency.appendNode("artifactId", dep.name)
                            dependency.appendNode("version", version)
                            dependency.appendNode("optional", "true")
                        }
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = "${rootDir}/build/staging-deploy"
        }
    }
}
