buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.rodnansol:spring-configuration-property-documenter-gradle-plugin:${springConfigPropertyDocumenterPluginVersion}"
    }
}

plugins {
    id "org.springframework.boot" version "${springBootVersion}" apply false
    id "io.spring.dependency-management" version "${springDependencyManagementPluginVersion}" apply false
    id "com.diffplug.spotless" version "${spotlessVersion}" apply false
    id "com.google.protobuf" version "${protobufPluginVersion}" apply false
    id "org.graalvm.buildtools.native" version "${graalvmBuildToolsVersion}" apply false
    id "net.ltgt.errorprone" version "${errorPronePluginVersion}" apply false
    id "io.github.danielliu1123.deployer" version "${deployerVersion}"
}

deploy {
    dirs = subprojects.stream()
            .map(e -> e.layout.buildDirectory.dir("repo").get().getAsFile())
            .filter(e -> e.exists())
            .toList()
    username = System.getenv("MAVENCENTRAL_USERNAME")
    password = System.getenv("MAVENCENTRAL_PASSWORD")
    publishingType = io.github.danielliu1123.deployer.PublishingType.WAIT_FOR_PUBLISHED
}

//allprojects { // uncomment this if you want to get IDE support, only for debugging purpose!
subprojects {
    if (it.name == "grpc-starter-dependencies") {
        // skip the bom project
        return
    }

    apply plugin: "java"
    apply plugin: "java-library"

    java {
        sourceCompatibility = 17
        targetCompatibility = 17
    }

    javadoc {
        // Suppress warnings about missing Javadoc comments
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    configurations {
        optional
        compileOnly.extendsFrom optional
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs += ["-parameters"]
        // https://github.com/uber/NullAway/wiki/JSpecify-Support#supported-jdk-versions
        options.compilerArgs += ["-XDaddTypeAnnotationsToSymbol=true"]
        options.encoding = "UTF-8"
    }

    test {
        systemProperties("spring.cloud.compatibility-verifier.enabled": "false")
        useJUnitPlatform()

        jvmArgs("-XX:+EnableDynamicAgentLoading")
    }

    afterEvaluate {
        tasks.findByName("bootRun")?.configure {
            systemProperty("spring.cloud.compatibility-verifier.enabled", "false")
        }
    }

    // dependency management
    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "com.google.protobuf:protobuf-bom:${protobufVersion}"
            mavenBom "io.grpc:grpc-bom:${grpcVersion}"
        }
    }

    dependencies {
        compileOnly("org.projectlombok:lombok")
        annotationProcessor("org.projectlombok:lombok")
        testCompileOnly("org.projectlombok:lombok")
        testAnnotationProcessor("org.projectlombok:lombok")
        // https://docs.gradle.org/current/userguide/upgrading_version_8.html#test_framework_implementation_dependencies
        testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    }

    // spotless
    apply plugin: "com.diffplug.spotless"
    spotless {
        encoding "UTF-8"
        java {
            toggleOffOn()
            removeUnusedImports()
            trimTrailingWhitespace()
            endWithNewline()
            palantirJavaFormat()
            importOrder()
            formatAnnotations()

            targetExclude "**/generated/**"

            custom("Refuse wildcard imports", {
                if (it =~ /\nimport .*\*;/) {
                    throw new IllegalStateException("Do not use wildcard imports, 'spotlessApply' cannot resolve this issue, please fix it manually.")
                }
            } as Closure<String>)
        }
    }

    apply plugin: "net.ltgt.errorprone"
    dependencies {
        errorprone "com.google.errorprone:error_prone_core:${errorProneCoreVersion}"
        errorprone "com.uber.nullaway:nullaway:${nullAwayVersion}"
    }

    tasks.withType(JavaCompile).configureEach {
        options.errorprone {
            // https://github.com/tbroyer/gradle-errorprone-plugin?tab=readme-ov-file#properties
            excludedPaths = ".*/(generated|examples)/.*"
            // https://github.com/uber/NullAway/wiki/Configuration
            check("NullAway", net.ltgt.gradle.errorprone.CheckSeverity.ERROR)
            option("NullAway:AnnotatedPackages", "grpcstarter")
            option("NullAway:HandleTestAssertionLibraries", "true")
        }
    }
}

apply from: "${rootDir}/gradle/generate-configuration-properties-docs.gradle"
