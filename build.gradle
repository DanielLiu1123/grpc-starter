buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        // https://github.com/rodnansol/spring-configuration-property-documenter
        classpath "org.rodnansol:spring-configuration-property-documenter-gradle-plugin:+"
    }
}

plugins {
    id "org.springframework.boot" version "${springBootVersion}" apply false
    id "io.spring.dependency-management" version "+" apply false
    id "com.diffplug.spotless" version "${spotlessVersion}" apply false
    id "com.github.spotbugs" version "${spotbugsVersion}" apply false
    // https://github.com/google/protobuf-gradle-plugin
    id "com.google.protobuf" version "+" apply false
    // https://github.com/graalvm/native-build-tools
    id "org.graalvm.buildtools.native" version "${graalvmBuildToolsVersion}" apply false
    id "org.jreleaser" version "${jReleaserVersion}" apply false
    // https://github.com/spring-gradle-plugins/nullability-plugin
    id "io.spring.nullability" version "+" apply false
}

//allprojects { // uncomment this if you want to get IDE support, only for debugging purpose!
subprojects {
    if (it.name == "grpc-starter-dependencies") {
        // skip the bom project
        return
    }

    apply plugin: "java"
    apply plugin: "java-library"

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    javadoc {
        // Suppress warnings about missing Javadoc comments
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    configurations {
        optional
        compileOnly.extendsFrom optional
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = "https://repo.spring.io/snapshot" }
        maven { url = "https://repo.spring.io/milestone" }
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs += ["-parameters"]
        // https://github.com/uber/NullAway/wiki/JSpecify-Support#supported-jdk-versions
        options.compilerArgs += ["-XDaddTypeAnnotationsToSymbol=true"]
        options.encoding = "UTF-8"
    }

    test {
        systemProperties("spring.cloud.compatibility-verifier.enabled": "false")
        useJUnitPlatform()

        jvmArgs("-XX:+EnableDynamicAgentLoading")
    }

    afterEvaluate {
        tasks.findByName("bootRun")?.configure {
            systemProperty("spring.cloud.compatibility-verifier.enabled", "false")
        }
    }

    // dependency management
    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "com.google.protobuf:protobuf-bom:${protobufVersion}"
            mavenBom "io.grpc:grpc-bom:${grpcVersion}"
        }
    }

    dependencies {
        compileOnly("org.projectlombok:lombok:+")
        annotationProcessor("org.projectlombok:lombok:+")
        testCompileOnly("org.projectlombok:lombok:+")
        testAnnotationProcessor("org.projectlombok:lombok:+")
        compileOnly("com.github.spotbugs:spotbugs-annotations:${spotbugsAnnotationsVersion}")
        // https://docs.gradle.org/current/userguide/upgrading_version_8.html#test_framework_implementation_dependencies
        testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    }

    // spotless
    apply plugin: "com.diffplug.spotless"
    spotless {
        encoding "UTF-8"
        java {
            toggleOffOn()
            removeUnusedImports()
            trimTrailingWhitespace()
            endWithNewline()
            palantirJavaFormat()

            targetExclude "**/generated/**"

            custom("Refuse wildcard imports", {
                if (it =~ /\nimport .*\*;/) {
                    throw new IllegalStateException("Do not use wildcard imports, 'spotlessApply' cannot resolve this issue, please fix it manually.")
                }
            } as Closure<String>)
        }
    }

    // spotbugs
    apply plugin: "com.github.spotbugs"
    spotbugs {
        spotbugsTest.enabled = false
        omitVisitors.addAll "FindReturnRef", "DontReusePublicIdentifiers"
        excludeFilter = file("${rootDir}/config/spotbugs/exclude.xml")
    }

    apply plugin: "io.spring.nullability"
}

apply from: "${rootDir}/gradle/generate-configuration-properties-docs.gradle"
apply from: "${rootDir}/gradle/jreleaser.gradle"
