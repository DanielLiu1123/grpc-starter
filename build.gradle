plugins {
    id 'org.springframework.boot' version "${springBootVersion}" apply false
    id 'io.spring.dependency-management' version "${springDependencyManagementVersion}" apply false
    id "com.google.protobuf" version "${protobufGradlePluginVersion}" apply false
    id 'com.diffplug.spotless' version "${spotlessVersion}" apply false
    id "com.github.spotbugs" version "${spotbugsVersion}" apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://repo.spring.io/snapshot"
        }
        maven {
            url "https://repo.spring.io/milestone"
        }
    }
    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << '-parameters'
    }
    compileTestJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << '-parameters'
    }
    test {
        useJUnitPlatform()
    }
    // dependency management
    apply plugin: 'io.spring.dependency-management'
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }
    }
    // spotless
    apply plugin: 'com.diffplug.spotless'
    spotless {
        encoding 'UTF-8'
        java {
            toggleOffOn()
            removeUnusedImports()
            trimTrailingWhitespace()
            endWithNewline()
            palantirJavaFormat()

            targetExclude "build/generated/**"
        }
    }
    // spotbugs
    apply plugin: 'com.github.spotbugs'
    spotbugs {
        spotbugsTest.enabled = false
        omitVisitors.addAll 'FindReturnRef', 'MethodReturnCheck'
    }
}
